import org.gradle.internal.os.OperatingSystem

plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0"
}

apply plugin: 'java'

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

repositories {
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['../../../java']
if (!OperatingSystem.current().isMacOsX()) {
    sourceSets.main.java.excludes += ['org/cef/browser/mac/**']
}

sourceSets.test.java.srcDirs = ['../../../java_tests']
sourceSets.test.resources.srcDirs = ['../../../java_tests/tests/junittests/data']
sourceSets.test.output.resourcesDir = 'build/classes/java/test/tests/junittests/data'

def separator = ":"
if (OperatingSystem.current().isWindows()) {
    separator = ";"
}
def mpaths = "$projectDir/../../../third_party/jogamp/jar" +
        separator + "$projectDir/../../../third_party/thrift" +
        separator + "$projectDir/../../../third_party/slf4j" +
        separator + "$projectDir/../../../third_party/jogamp/jar/gluegen-rt.jar" +
        separator + "$projectDir/../../../third_party/thrift/libthrift-0.19.0.jar" +
        separator + "$projectDir/../../../third_party/slf4j/slf4j-api-2.0.0.jar" +
        separator + "$projectDir/../../../third_party/jogamp/jar/jogl-all.jar"
compileJava.options.compilerArgs += [
    '--module-path', mpaths,
    '--add-modules', 'jogl.all,gluegen.rt,org.slf4j,org.apache.thrift',
    '--add-exports', 'jogl.all/com.jogamp.nativewindow=jcef',
    '--add-exports', 'jogl.all/com.jogamp.opengl.awt=jcef',
    '--add-exports', 'jogl.all/com.jogamp.opengl=jcef',
    '--add-exports', 'jogl.all/com.jogamp.opengl.util=jcef',
    '--add-exports', 'java.desktop/sun.awt=jcef',
    '--add-exports', 'java.desktop/java.awt.peer=jcef',
    '--add-exports', 'java.desktop/sun.lwawt.macosx=jcef',
    '--add-exports', 'java.desktop/sun.lwawt=jcef']

def jbr_java = test.executable
if (OperatingSystem.current().isWindows() && project.hasProperty('jbr_win')) {
    jbr_java = jbr_win + '/bin/java.exe'
} else if (OperatingSystem.current().isMacOsX() && project.hasProperty('jbr_mac')) {
    jbr_java = jbr_mac + '/bin/java'
} else if (OperatingSystem.current().isLinux() && project.hasProperty('jbr_linux')) {
    jbr_java = jbr_linux + '/bin/java'
}

test {
    useJUnitPlatform()
    include 'tests/junittests/**'
    include 'tests/basic/**'

    failFast = true
    jvmArgs = ['-Djcef.junittests.isGradleLauncher=true',
               '--add-opens=java.desktop/java.awt.peer=ALL-UNNAMED',
               '--add-exports=java.desktop/java.awt.peer=ALL-UNNAMED',
               '--add-opens=java.desktop/sun.awt=ALL-UNNAMED',
               '--add-opens=java.desktop/java.awt.event=ALL-UNNAMED',
               '--add-exports=java.desktop/sun.awt=ALL-UNNAMED',
               '-Djcef.tests.osr=true'
    ]
    executable = jbr_java
}

dependencies {
    implementation files(
        "$projectDir/../../../third_party/jogamp/jar/jogl-all.jar",
        "$projectDir/../../../third_party/jogamp/jar/gluegen-rt.jar",
        "$projectDir/../../../third_party/slf4j/slf4j-api-2.0.0.jar",
        "$projectDir/../../../third_party/thrift/libthrift-0.19.0.jar"
    )
    testImplementation files(
        "$projectDir/../../../third_party/junit/junit-platform-console-standalone-1.10.0.jar",
        "$projectDir/../../../third_party/gson/gson-2.10.1.jar",
        "$projectDir/../../../third_party/thrift/libthrift-0.19.0.jar",
        "$projectDir/../../../third_party/jakarta/jakarta.xml.bind-api-2.3.2.jar",
        "$projectDir/../../../third_party/jbrapi/jbr-api-17.1.10.0.jar"
    )
}
